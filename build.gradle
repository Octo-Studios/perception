plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.mod_group
    version = rootProject.mod_version + "+" + rootProject.minecraft_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        archivesName = "$rootProject.mod_name" + "-" + "$project.name".toUpperCase()
    }

    repositories {
        mavenLocal()
        flatDir {
            dirs 'libs'
        }
        maven {
            name = "OctoStudios"
            url = uri("https://maven.octo-studios.com/releases")
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"

        loom {
            silentMojangMappingsLicense()
        }

        mappings loom.officialMojangMappings()

        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'

        testCompileOnly 'org.projectlombok:lombok:1.18.32'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

        implementation 'org.jetbrains:annotations:20.1.0'

        if (file("libs").isDirectory()) {
            file("libs").eachFile { file ->
                def splitPos = file.name.lastIndexOf("-")

                if (file.name != "desktop.ini") {
                    println file.name

                    def modartifact = file.name.substring(0, splitPos)
                    def modversion = file.name.substring(splitPos + 1, file.name.length() - 4)
                    def modreference = "lib:$modartifact:$modversion"

                    dependencies {
                        implementation (project.dependencies.create(modreference) {
                            transitive = false
                        })
                    }
                }
            }
        } else file("libs").mkdir()
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        repositories {
            maven {
                name = "octo"
                url = "https://maven.octo-studios.com/releases"
                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId = group
                artifactId = project.mod_id + "-" + project.name
                version = version + "+" + project.minecraft_version
                from components.java
            }
        }
    }
}
